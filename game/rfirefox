#!/usr/bin/env zsh

set -e

profile=${1:-default}
FIREFOX=~/.mozilla/firefox/

cd ~/rawdisk/ff/.mozilla/firefox
dl=(*.$profile(N))
if [ -z $dl ] || [ ! -d $dl ] ; then
  ar=($FIREFOX/*.$profile.zst(N))
  if [[ -z $ar ]] ; then
    dl=($FIREFOX/*.$profile)
    d=$dl[1]
    cp -R --preserve=all $d .
    d=$d:t
  else
    ar=$ar[1]
    cp $ar .
    ar=${ar:t}
    tar --zstd -xf $ar
    d=${ar:0:-3}
  fi
else
  d=$dl[1]
fi

ulimit -Sv 4800000000

HOME=~/rawdisk/ff firefox -P $profile $@[2,-1]
[[ $(($RANDOM % 10)) == 1 ]] && for f in $d/**/*.sqlite ; do sqlite3 "$f" 'VACUUM;'; done ||:
ZSTD_CLEVEL=4 tar --zstd -cf $d.zst $d
mv $FIREFOX/$d.zst.old $FIREFOX/$d.zst.old.old ||:
mv $FIREFOX/$d.zst $FIREFOX/$d.zst.old ||:
mv $d.zst $FIREFOX/

backup=~/rawdisk2/${(%):-%D_%*}-$profile.tabs.txt
dejsonlz4 $d/sessionstore.jsonlz4 | jq -r '.windows[].tabs[].entries[].url' > $backup
7zr a ~/STG-backups/stg-backups.7z $backup
rm $backup
