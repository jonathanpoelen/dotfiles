#!/usr/bin/env zsh

set -e

zparseopts -E -D -Aparams h help t b: s: p n:

if ((${+params[-h]} || ${+params[-help]})); then
  >&0 <<<"$0 [ -h | --help ] [-t [-b BASE] [-s TIME_SIZE]] [-p] [-n MAX_PAGE] directories...
  -t             add timestamp
  -b BASE        base format between 2 and 66 inclusive (O-9a-zA-Z@#~={}()[])
  -s TIME_SIZE   indicates how many characters should be displayed timestamp
  -p             add number of page
  -n MAX_PAGE    split into several pdf of manixum MAX_PAGE pages"
  exit 0
fi

(( $# == 0 )) && dirs=(*(/)) || dirs=($@)

declare -i \
  max_page=${params[-n]} \
  add_page_number=${+params[-p]}

if (( max_page <= 0 )); then
  max_page=9999999
fi

# timestamp
if (( ${+params[-t]} )); then
  timestamp=${(%):-%D{%s}}

  # base 10 to [2-66]
  declare -i base=${params[-b]}
  if (( 2 <= base && base <= 66 )); then
    declare chars='0123456789abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ@#~={}()[]'
    declare -i t=$timestamp r
    chars=${chars:0:$base}
    timestamp=
    while (( t > 0 )); do
      ((r=t%base, t/=base, 1))
      timestamp=${chars[r+1]}$timestamp
    done
  fi

  # set timestamp char
  if (( ${+params[-s]} )); then
    timestamp=-${(l:${params[-s]}::0:)timestamp}
  else
    timestamp=-$timestamp
  fi
else
  timestamp=
fi

for d in $dirs ; do
  output=$d:t
  echo $output

  images=($d/*/*(NL+0))
  images=(${(on)images})

  if (( $#images >= max_page )); then
    # split into several pdf
    for (( n=1, i=1; i <= $#images; i+=max_page, ++n)); do
      if (( add_page_number )); then
        nbpage=-$((i + max_page - 1 < $#images ? max_page : $#images - i))
      else
        nbpage=
      fi
      img2pdf --output $output$timestamp-$n$nbpage.pdf $images[$i,$(($i+max_page-1))]
    done
  elif (( add_page_number )); then
    img2pdf --output $output$timestamp-$#images.pdf $images
  else
    img2pdf --output $output$timestamp.pdf $images
  fi
done
