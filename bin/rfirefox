#!/usr/bin/env zsh

# Open firefox in a memory directory.
# At the end of a session, the profile folder is compressed into a zst archive.
#
# Backup tabs url are in ~/STG-backups/$profile.backups.7z

set -ex

profile=${1:-default}
FIREFOX=~/.mozilla/firefox/

cd ~/rawdisk/ff/.mozilla/firefox
dl=(*.$profile(N))
if [[ -z $dl ]] || [[ ! -d $dl ]] ; then
  ar=($FIREFOX/*.$profile.zst(N))
  if [[ -z $ar ]] ; then
    dl=($FIREFOX/*.$profile)
    d=$dl[1]
    cp -R --preserve=all $d .
    d=$d:t
  else
    ar=$ar[1]
    tar --zstd -xf $ar
    ar=${ar:t}
    d=${ar:0:-4}
  fi
else
  d=$dl[1]
fi

ulimit -Sv 4800000000

HOME=~/rawdisk/ff firefox -P $profile $@[2,-1]
# optimize sqlite files
if [[ $(($RANDOM % 10)) == 1 ]]; then
  for f in $d/**/*.sqlite ; do
    sqlite3 "$f" 'VACUUM;'
  done
fi
ZSTD_CLEVEL=4 tar --zstd -cf $d.zst $d
# rotate zst files (3 files)
mv $FIREFOX/$d.zst.old $FIREFOX/$d.zst.old.old ||:
mv $FIREFOX/$d.zst $FIREFOX/$d.zst.old ||:
mv $d.zst $FIREFOX/

# extract and save tabs url
backup=~/rawdisk2/${(%):-%D_%*}-$profile.tabs.txt
dejsonlz4 $d/sessionstore.jsonlz4 | jq -r '.windows[].tabs[].entries[].url' > $backup
7zr a ~/STG-backups/$profile.backups.7z $backup
rm $backup
